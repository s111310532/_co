class Paddle {
    field int x, y; // Posisi
    field int width, height;

    // Constructor: Lahirin object
    constructor Paddle new(int Ax, int Ay, int Awidth, int Aheight) {
        let x = Ax;
        let y = Ay;
        let width = Awidth;
        let height = Aheight;
        do draw();
        return this;
    }

    // Buang memori (Jack ga ada Garbage Collector!)
    method void dispose() {
        do Memory.deAlloc(this);
        return;
    }

    // Gambar kotak hitam
    method void draw() {
        do Screen.setColor(true);
        do Screen.drawRectangle(x, y, x + width, y + height);
        return;
    }

    // Hapus kotak (gambar putih) sebelum gerak
    method void erase() {
        do Screen.setColor(false);
        do Screen.drawRectangle(x, y, x + width, y + height);
        return;
    }

    // Gerak Kiri
    method void moveLeft() {
        if (x > 4) {
            do erase(); // Hapus posisi lama
            let x = x - 4; // Pindah
            do draw();  // Gambar posisi baru
        }
        return;
    }

    // Gerak Kanan
    method void moveRight() {
        if ((x + width) < 508) {
            do erase();
            let x = x + 4;
            do draw();
        }
        return;
    }
    // --- TAMBAHAN BUAT COLLISION ---
    method int getLeft() { return x; }
    method int getRight() { return x + width; }
    method int getTop() { return y; }
    method int getBottom() { return y + height; }
}
